# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.release
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (debug+release libs)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'ninja')
options=('!strip')
provides=('corrade-git')

build() {
    mkdir -p "$startdir/build"
    cd "$startdir/build"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=TRUE \
        -DWITH_FIND_MODULE=TRUE \
        -G Ninja
    ninja

    mkdir -p "$startdir/build-release"
    cd "$startdir/build-release"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=TRUE \
        -DWITH_FIND_MODULE=TRUE \
        -G Ninja
    ninja
}

check() {
    cd "$startdir/build"
    ctest --output-on-failure

    cd "$startdir/build-release"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build"
    DESTDIR="$pkgdir/" ninja install

    cd "$startdir/build-release"
    DESTDIR="$pkgdir/" ninja install/strip
}
