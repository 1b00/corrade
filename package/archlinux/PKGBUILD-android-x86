# Author: mosra <mosra@centrum.cz>
pkgname=android-x86-corrade
pkgver=dev
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (Android x86)"
arch=('any')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'ninja')
options=(!strip !buildflags staticlibs)

build() {
    if [ ! -d "$startdir/build-android-x86" ] ; then
        mkdir "$startdir/build-android-x86"
        cd "$startdir/build-android-x86"

        cmake .. \
            -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
            -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/Android-x86.cmake" \
            -G Ninja
    fi

    cd "$startdir/build-android-x86"

   cmake .. \
       -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
       -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/Android-x86.cmake" \
       -DCMAKE_BUILD_TYPE=Release \
       -DCMAKE_INSTALL_PREFIX=/opt/android-ndk/platforms/android-19/arch-x86/usr
    ninja
}

package() {
    cd "$startdir/build-android-x86"
    DESTDIR="$pkgdir/" ninja install/strip
}
