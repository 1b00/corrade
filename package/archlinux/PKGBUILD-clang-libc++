# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.clang.libc++
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (built with Clang and libc++)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'ninja' 'clang' 'libc++')
options=(!strip)
provides=('corrade-git')

build() {
    if [ ! -d "$startdir/build-clang-libc++" ] ; then
        mkdir "$startdir/build-clang-libc++"
        cd "$startdir/build-clang-libc++"

        cmake .. \
            -DCMAKE_CXX_COMPILER=clang++ \
            -DCMAKE_CXX_FLAGS="-std=c++11 -stdlib=libc++" \
            -DCMAKE_EXE_LINKER_FLAGS=-lc++abi \
            -G Ninja
    fi

    cd "$startdir/build-clang-libc++"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$startdir/build-clang-libc++"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-clang-libc++"
    DESTDIR="$pkgdir/" ninja install
}
