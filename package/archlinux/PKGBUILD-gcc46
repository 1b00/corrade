# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.gcc46
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (built with GCC 4.6)"
arch=('i686' 'x86_64')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'ninja' 'gcc46')
options=(!strip)
provides=('corrade-git')

build() {
    if [ ! -d "$startdir/build-gcc46" ] ; then
        mkdir "$startdir/build-gcc46"
        cd "$startdir/build-gcc46"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.6 \
            -G Ninja
    fi

    cd "$startdir/build-gcc46"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTS=ON
    ninja
}

check() {
    cd "$startdir/build-gcc46"
    ctest --output-on-failure
}

package() {
    cd "$startdir/build-gcc46"
    DESTDIR="$pkgdir/" ninja install
}
