# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-corrade
pkgver=dev
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (Emscripten)"
arch=('any')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('cmake' 'ninja')
options=(!strip !buildflags)

build() {
    if [ ! -d "$startdir/build-emscripten" ] ; then
        mkdir "$startdir/build-emscripten"
        cd "$startdir/build-emscripten"

        cmake .. \
            -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
            -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$startdir/build-emscripten"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/emscripten/system
    ninja
}

package() {
    cd "$startdir/build-emscripten"
    DESTDIR="$pkgdir/" ninja install
}
