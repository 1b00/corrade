# Author: mosra <mosra@centrum.cz>
pkgname=nacl-corrade
pkgver=dev.glibc
pkgrel=1
pkgdesc="Multiplatform plugin management and utility library (NaCl glibc version)"
arch=('any')
url="http://mosra.cz/blog/corrade.php"
license=('MIT')
makedepends=('nacl-sdk' 'cmake')
options=(!buildflags !strip)

build() {
    # Build 32bit
    mkdir -p "$startdir/build-nacl-x86-32"
    cd "$startdir/build-nacl-x86-32"

    cmake .. \
        -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/NaCl-glibc-x86-32.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DGCC44_COMPATIBILITY=ON \
        -DLIB_SUFFIX=/32
    make

    # Build 64bit
    mkdir -p "$startdir/build-nacl-x86-64"
    cd "$startdir/build-nacl-x86-64"

    cmake .. \
        -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/NaCl-glibc-x86-64.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DGCC44_COMPATIBILITY=ON
    make
}

package() {
    # Install 32bit
    cd "$startdir/build-nacl-x86-32"
    make DESTDIR="$pkgdir/" install

    # Install 64bit (the headers will be overwritten, but they are (and should
    # be) the same for both versions
    cd "$startdir/build-nacl-x86-64"
    make DESTDIR="$pkgdir/" install
}
